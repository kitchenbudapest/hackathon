## INFO ##
## INFO ##

# Indicate the beginning of building
print '{:#^80}'.format(' SCONS BUILD ')

# Import python modules
from os.path   import join
from itertools import chain


#------------------------------------------------------------------------------#
# If developer mode passed ($ scons devel=yes)
DEVEL = ARGUMENTS.get('devel', False)



# Set global variables
#------------------------------------------------------------------------------#
# C compiler
CC = CC = ARGUMENTS.get('cc', 'clang')

# C compiler (generic) flags
CCFLAGS = ['v',
           'g',
           'O3',
           'Wall',
           'Wextra',
           'pedantic',
           'std=c99',]

# GCC specific c compiler flags
GCCFLAGS = ['fdiagnostics-color=always']
GCCFLAGS_DEVEL = []

# CLANG specific  compiler flags
CLANGFLAGS = ['fcolor-diagnostics',
              'fmacro-backtrace-limit=0']
CLANGFLAGS_DEVEL = ['Weverything']



# Set flags based on the above variables
#------------------------------------------------------------------------------#
if CC.startswith('clang'):
    if DEVEL:
        CLANGFLAGS.extend(CLANGFLAGS_DEVEL)
    flags = CLANGFLAGS
elif CC.startswith('gcc'):
    if DEVEL:
        GCCFLAGS.extend(GCCFLAGS_DEVEL)
    flags = GCCFLAGS
else:
    raise ValueError('CC is not supported: {}'.format(CC))

# Finalize flags
CCFLAGS = ['-' + flag for flag in chain(CCFLAGS, flags)]


# Helper functions
#------------------------------------------------------------------------------#
def path_prefixer(folder, files):
    return [join(folder, file) for file in files]



# Target: building duktape as a library
#------------------------------------------------------------------------------#
def kb():
    # Output and inputs
    INPUT_DIR  = '.'
    OUTPUT_DIR = 'build'
    TARGET     = join('libs', 'kb')
    SOURCE     = [join('src', 'errors.c'),
                  join('src', 'utils', 'dense_set.c'),
                  join('src', 'rpi2', 'contexts.c'),
                  join('src', 'rpi2', 'events.c'),
                  join('src', 'rpi2', 'pins.c'),
                  join('src', 'rpi2', 'sensors.c'),
                  join('src', 'rpi2', 'sensors', 'four_keys.c'),
                  join('src', 'rpi2', 'sensors', 'leds.c'),
                  join('src', 'rpi2', 'sensors', 'rgb_leds.c'),
                  join('src', 'rpi2', 'sensors', 'step_motors.c'),
                  join('src', 'rpi2', 'sensors', 'photo_resistors.c'),
                  join('src', 'rpi2', 'sensors', 'rotary_encoders.c'),
                  join('src', 'rpi2', 'sensors', 'motion_sensors.c'),
                  join('src', 'rpi2', 'sensors', 'ultrasonic_sensors.c'),
                  join('src', 'rpi2', 'sensors', 'hall_effects.c'),]

    # C pre-processor path (include)
    CPPPATH = [join(INPUT_DIR, 'include')]
    # Library path
    LIBPATH = [INPUT_DIR,
               '/usr/lib',
               '/usr/local/lib']
    # Libraries
    LIBS    = ['jemalloc',
               'm']

    # If this is not a desktop build
    if not ARGUMENTS.get('desktop', False):
        LIBS.append('bcm2835')

    # Create environment
    environment = Environment(CC      = CC,
                              CCFLAGS = CCFLAGS,
                              CPPPATH = CPPPATH,
                              LIBPATH = LIBPATH,
                              LIBS    = LIBS)

    # Specify output directory
    environment.VariantDir(variant_dir = OUTPUT_DIR,
                           src_dir     = INPUT_DIR)

    # Build
    build = environment.SharedLibrary(target = TARGET,
                                      source = path_prefixer(OUTPUT_DIR,
                                                             SOURCE))

    # Name this build
    environment.Alias('kb', build)
    Default(build)


#------------------------------------------------------------------------------#
# Create build options
kb()
