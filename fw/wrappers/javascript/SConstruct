## INFO ##
## INFO ##

# Indicate the beginning of building
print '{:#^80}'.format(' SCONS BUILD ')

# Import python modules
from os.path   import join
from itertools import chain


#------------------------------------------------------------------------------#
# If developer mode passed ($ scons devel=yes)
DEVEL = ARGUMENTS.get('devel', False)



# Set global variables
#------------------------------------------------------------------------------#
# C compiler
CC = ARGUMENTS.get('cc', 'clang')

# C compiler (generic) flags
CCFLAGS = ['v',
           'g',
           'O3',
           'Wall',
           'Wextra',
           'pedantic',
           'std=c99',]

# GCC specific c compiler flags
GCCFLAGS = ['fdiagnostics-color=always']
GCCFLAGS_DEVEL = []

# CLANG specific  compiler flags
CLANGFLAGS = ['fcolor-diagnostics',
              'fmacro-backtrace-limit=0']
CLANGFLAGS_DEVEL = ['Weverything']



# Set flags based on the above variables
#------------------------------------------------------------------------------#
if CC.startswith('clang'):
    if DEVEL:
        CLANGFLAGS.extend(CLANGFLAGS_DEVEL)
    flags = CLANGFLAGS
elif CC.startswith('gcc'):
    if DEVEL:
        GCCFLAGS.extend(GCCFLAGS_DEVEL)
    flags = GCCFLAGS
else:
    raise ValueError('CC is not supported: {}'.format(CC))

# Finalize flags
CCFLAGS = ['-' + flag for flag in chain(CCFLAGS, flags)]


# Helper functions
#------------------------------------------------------------------------------#
def path_prefixer(folder, files):
    return [join(folder, file) for file in files]



# Target: building duktape as a library
#------------------------------------------------------------------------------#
def duktape():
    # Output and inputs
    INPUT_DIR  = join('duktape-1.3.0', 'src')
    OUTPUT_DIR = join('build', 'duktape')
    TARGET     = join('libs', 'duktape')
    SOURCE     = ['duktape.c']

    # C pre-processor path (include)
    CPPPATH = [INPUT_DIR]
    # Library path
    LIBPATH = [INPUT_DIR,
               '/usr/lib',
               '/usr/local/lib']
    # Libraries
    LIBS    = ['m']

    # Create environment
    environment = Environment(CC      = CC,
                              CCFLAGS = CCFLAGS,
                              CPPPATH = CPPPATH,
                              LIBPATH = LIBPATH,
                              LIBS    = LIBS)

    # Specify output directory
    environment.VariantDir(variant_dir = OUTPUT_DIR,
                           src_dir     = INPUT_DIR)

    # Build
    build = environment.SharedLibrary(target = TARGET,
                                      source = path_prefixer(OUTPUT_DIR,
                                                             SOURCE))

    # Name this build
    environment.Alias('duktape', build)



# Target: building javascript wrapper
#------------------------------------------------------------------------------#
def wrapper():
    # Output and inputs
    INPUT_DIR  = '.'
    OUTPUT_DIR = join('build', 'kbjs')
    TARGET     = join('bins', 'kbjs')
    SOURCE     = ['kbjs.c',
                  join('src', 'built_ins.c'),
                  join('src', 'errors.c'),
                  join('src', 'types.c'),
                  join('src', 'pin_ids.c'),
                  join('src', 'js_types.c'),
                  join('src', 'contexts.c'),
                  join('src', 'events.c'),
                  join('src', 'sensors', 'leds.c'),
                  join('src', 'sensors', 'four_keys.c'),
                  join('src', 'sensors', 'rgb_leds.c'),
                  join('src', 'sensors', 'step_motors.c'),
                  join('src', 'sensors', 'photo_resistors.c'),
                  join('src', 'sensors', 'rotary_encoders.c'),
                  join('src', 'sensors', 'motion_sensors.c'),
                  join('src', 'sensors', 'ultrasonic_sensors.c'),
                  join('src', 'sensors', 'hall_effects.c'),]

    # C pre-processor path (include)
    CPPPATH = ['.',
               '/usr/include',
               '/usr/local/include']
    # Library path
    LIBPATH = ['.',
               '/usr/lib',
               '/usr/local/lib']
    # Libraries
    LIBS    = ['duktape',
               'jemalloc',
               'kb',
               'm']

    # If this is not a desktop build
    if not ARGUMENTS.get('desktop', False):
        LIBS.append('bcm2835')

    # Create environment
    environment = Environment(CC      = CC,
                              CCFLAGS = CCFLAGS,
                              CPPPATH = CPPPATH,
                              LIBPATH = LIBPATH,
                              LIBS    = LIBS)

    # Specify output directory
    environment.VariantDir(variant_dir = OUTPUT_DIR,
                           src_dir     = INPUT_DIR)

    # Build (and set it as the defaulkt target
    build = environment.Program(target = TARGET,
                                source = path_prefixer(OUTPUT_DIR,
                                                       SOURCE))

    # Name this build
    environment.Alias('kbjs', build)
    Default(build)



#------------------------------------------------------------------------------#
# Create build options
if 'duktape' in COMMAND_LINE_TARGETS:
    duktape()
else:
    wrapper()
